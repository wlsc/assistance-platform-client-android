apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

ext {
    appName = 'assistance-tud-edition'
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    defaultConfig {
        applicationId 'de.tudarmstadt.informatik.tk.assistance'
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 53
        versionName "1.4.2"

        multiDexEnabled true

        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def newName = output.outputFile.name
                    newName = newName.replace("app-", appName + "-")
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }
        }
        debug {
            debuggable true
            minifyEnabled false
        }
    }
    dexOptions {
        javaMaxHeapSize '4g'
        preDexLibraries = false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // multidex support
    compile 'com.android.support:multidex:1.0.1'
    // sensor information gathering
    compile project(':AssistanceSDK')
    // support libraries
    compile 'com.android.support:support-v13:23.4.0'
    compile 'com.android.support:preference-v14:23.4.0'
    compile 'com.android.support:support-annotations:23.4.0'
    compile 'com.android.support:design:23.4.0'
    // show cases (tutorials) lib
    compile 'com.github.amlcurran.showcaseview:library:5.4.3'
    // dependency injection on compile
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    // effective picture loader
    compile 'com.squareup.picasso:picasso:2.5.2'
    // linear layout manager that supports WRAP_CONTENT
    compile 'org.solovyev.android.views:linear-layout-manager:0.5@aar'
    compile 'com.infstory:switch-preference-compat:1.0.0'
    // memory leaks finder
//    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
//    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    // better unit testing for ui
    androidTestCompile 'com.android.support:support-annotations:23.4.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}